import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "jp.juggler.fadownloader"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 18
        versionName "1.12.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        rc {
        }
        rcAdFree {
        }
        dev {
            applicationId "jp.juggler.fadownloader.dev"
        }
    }


    // Generate Signed APK のファイル名を変更
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.each { output ->
                if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                    // Rename APK
                    def versionCode = defaultConfig.versionCode
                    def versionName = defaultConfig.versionName
                    def flavor = variant.flavorName
                    def date = new SimpleDateFormat("yyyyMMdd_HHmmss").format(new Date())
                    def newName = "FADownloader-${flavor}-${versionCode}-${versionName}-${date}.apk"
                    output.outputFile = new File((String) output.outputFile.parent, (String) newName)
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.google.android.gms:play-services-location:10.2.0'
    compile 'com.google.firebase:firebase-ads:10.2.0'

    compile project(':exif')
    compile project(':billing')

    testCompile 'junit:junit:4.12'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'commons-io:commons-io:2.4'
    compile 'com.neovisionaries:nv-websocket-client:1.31'

}

apply plugin: 'com.google.gms.google-services'
